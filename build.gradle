plugins {
    id 'application'
    id 'com.google.protobuf' version '0.8.18'
    id 'idea'
}

repositories {
    maven {
        url "https://maven-central.storage-download.googleapis.com/maven2/" }
    mavenCentral()
    mavenLocal()
}

sourceCompatibility = 1.9
targetCompatibility = 1.9

def grpcVersion = '1.48.0'
def protobufVersion = '3.21.1'
def protocVersion = protobufVersion

dependencies {
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    compileOnly "org.apache.tomcat:annotations-api:6.0.53"

    implementation "com.google.protobuf:protobuf-java-util:${protobufVersion}"
    implementation "com.azure:azure-messaging-eventhubs:5.7.0"

    implementation "io.jsonwebtoken:jjwt:0.9.1"

    runtimeOnly "io.grpc:grpc-netty-shaded:${grpcVersion}"

    testImplementation "io.grpc:grpc-testing:${grpcVersion}"
    testImplementation "junit:junit:4.12"
    testImplementation "org.mockito:mockito-core:3.4.0"
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${protocVersion}" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

startScripts.enabled = false

task alecServer(type: CreateStartScripts) {
    mainClass = 'org.opennms.aleccloud.AlecCloudServer'
    applicationName = 'alec-server'
    outputDir = new File(project.buildDir, 'tmp/scripts/' + name)
    classpath = startScripts.classpath
}

// task alecClient(type: CreateStartScripts) {
//     mainClass = 'org.opennms.aleccloud.AlecCloudTestClient'
//     applicationName = 'alec-test-client'
//     outputDir = new File(project.buildDir, 'tmp/scripts/' + name)
//     classpath = startScripts.classpath
// }

applicationDistribution.into('bin') {
    from(alecServer)
    // from(alecClient)
    fileMode = 0755
}
